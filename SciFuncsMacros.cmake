######################################################################
#
# SciFuncsMacros: Various functions and macros used by Tech-X scimake
#
# $Id$
#
# Copyright 2010-2012 Tech-X Corporation.
# Arbitrary redistribution allowed provided this copyright remains.
#
######################################################################

#
# SciPrintString: print a string in a status message as well as to
#   ${CONFIG_SUMMARY}
# Args:
#   str the string
#
macro(SciPrintString str)
  message(STATUS "${str}")
  if (DEFINED CONFIG_SUMMARY)
    file(APPEND "${CONFIG_SUMMARY}" "${str}\n")
  else ()
    message(WARNING "Variable CONFIG_SUMMARY is not defined, SciPrintString is unable to write to the summary file.")
  endif ()
endmacro()

#
# SciPrintVar: print a variable with standard formatting
# Args:
#   var the name of the variable
#
macro(SciPrintvar var)
  string(LENGTH "${var}" lens)
  math(EXPR lenb "35 - ${lens}")
  if (lenb GREATER 0)
    string(RANDOM LENGTH ${lenb} ALPHABET " " blstr)
  else ()
    set(blstr "")
  endif ()
  SciPrintString("  ${var}${blstr}= ${${var}}")
endmacro()

#
# Print all cmake variables generated by SciFindPackage
# Args:
#   pkg: the name of the package
#
macro(SciPrintCMakeResults pkg)
  # message("--------- RESULTS FOR ${pkg} ---------")
  SciPrintString("")
  SciPrintString("RESULTS FOR ${pkg}:")
  foreach (varsfx ROOT_DIR EXECUTABLES FILES INCLUDE_DIRS MODULE_DIRS LIBFLAGS LIBRARY_DIRS LIBRARY_NAMES LIBRARIES STLIBS)
    SciPrintVar(${pkg}_${varsfx})
  endforeach ()
  if (WIN32)
    SciPrintVar(${pkg}_DLLS)
  endif ()
endmacro()

#
# Print all autotools variables generated by SciFindPackage
# Args:
#   pkg: the name of the package
#
macro(SciPrintAutotoolsResults pkg)
  # message("--------- RESULTS FOR ${pkg} ---------")
  SciPrintString("")
  SciPrintString("RESULTS FOR ${pkg}:")
  foreach (varsfx ROOT_DIR DIR INCDIRS MODDIRS LIBS ALIBS)
    SciPrintVar(${pkg}_${varsfx})
  endforeach ()
  if (WIN32)
    SciPrintVar(${pkg}_DLLS)
  endif ()
endmacro()

#
# Create a pdf from Sphinx documentation
# Args:
#  MRST_FILE_BASE  the base name of the file
#
macro(SciCreateSphinxPdf rstFileBase exampleFiles)
  message(STATUS "Setting up pdf creation for ${rstFileBase}, which uses the files, ${exampleFiles}.")
  set(pdfdir ${CMAKE_CURRENT_BINARY_DIR}/pdf)
  set(texfile ${pdfdir}/${rstFileBase}.tex)
  set(pdffile ${pdfdir}/${rstFileBase}.pdf)
  add_custom_command(
    OUTPUT ${texfile}
    COMMAND ${Sphinx_EXECUTABLE} -b latex -c . ${SPHINX_MATHARG} -d
  ${CMAKE_CURRENT_BINARY_DIR}/doctrees ${CMAKE_CURRENT_SOURCE_DIR} ${pdfdir}
    DEPENDS ${exampleFiles}
  )
  add_custom_target(${rstFileBase}-latex DEPENDS ${texfile})
  # This must be make, as sphinx generates a unix makefile
  add_custom_command(
    OUTPUT ${pdffile}
    COMMAND make all-pdf
    DEPENDS ${texfile}
    WORKING_DIRECTORY ${pdfdir}
  )
  add_custom_target(${rstFileBase}-pdf ALL DEPENDS ${pdffile})
endmacro()

